plugins {
    id 'java'
    id 'java-library'
    id 'application'
    id 'idea'

    // Adds dependencyUpdates task
    id 'com.github.ben-manes.versions' version '0.28.0'
}

// Only necessary for debugging JARs locally
mainClassName = 'not-applicable'

group = 'EmbeddedVaadin'
version = '1.0-SNAPSHOT'

description = """"""

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

def gradleDependencyVersion = '6.3'

wrapper {
    gradleVersion = gradleDependencyVersion
    distributionType = Wrapper.DistributionType.ALL
}

repositories {
    mavenCentral()
    maven { url "http://maven.vaadin.com/vaadin-addons" }
    maven { url "https://plugins.gradle.org/m2/" }
}

def slf4jVersion = '1.7.30'
def jcabiVersion = '0.18.1'

def jettyVersion = '9.4.27.v20200227'
def daggerVersion = '2.26'
def vaadinVersion = '15.0.5'
def flowServerProductionModeVersion = '6.0.3'

dependencies {
    // Dagger code generation
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"

    // Dependency injection with Dagger
    implementation "com.google.dagger:dagger:$daggerVersion"

    implementation "org.slf4j:slf4j-log4j12:$slf4jVersion"
    implementation "com.jcabi:jcabi-log:$jcabiVersion"

    api "org.eclipse.jetty:jetty-continuation:$jettyVersion"
    api "org.eclipse.jetty:jetty-server:$jettyVersion"
    api "org.eclipse.jetty.websocket:websocket-server:$jettyVersion"
    api "org.eclipse.jetty.websocket:javax-websocket-server-impl:$jettyVersion"

    implementation enforcedPlatform("com.vaadin:vaadin-bom:$vaadinVersion")

    // Vaadin 14
    implementation("com.vaadin:vaadin-core") {
        // Webjars are only needed when running in Vaadin 13 compatibility mode
        ["com.vaadin.webjar", "org.webjars.bowergithub.insites",
         "org.webjars.bowergithub.polymer", "org.webjars.bowergithub.polymerelements",
         "org.webjars.bowergithub.vaadin", "org.webjars.bowergithub.webcomponents"]
                .forEach { group -> exclude(group: group) }
    }

    api "com.vaadin:flow-server-production-mode:$flowServerProductionModeVersion"

    api "javax.servlet:javax.servlet-api:4.0.1"
}
